grammar org.eclipse.scout.saml.Saml with org.eclipse.xtext.xbase.Xbase

generate saml "http://www.eclipse.org/scout/saml/Saml"

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

Model:
	module=ModuleElement
	(imports+=ImportElement)*
	(
		(translations+=TranslationElement)
		|
		(codes+=CodeElement)
		|
		(lookups+=LookupElement)
		|
		(templates+=TemplateElement)
		|
		(forms+=FormElement)
		|
		(logics+=LogicElement) /* for reference only, will not be traversed */
	)*;


// TYPES
QualifiedNameWithOptionalWildCard: QualifiedName  ('.' '*')?;
BooleanType: 'true'|'false';	//TODO: code completion

//TODO: code completion depending on element we are on
LogicEventType: 'all'|'modify_load'|'modify_store'|'modify_discard'|'new_load'|'new_store'|'new_discard'|'changed'|'click'|'master_changed'|'init'|'format_value'|'activated'|'load';

HorizontalAlignmentType:'left'|'center'|'right';	//TODO: code completion


ModuleElement: 'module' name=QualifiedName
	(
		('client' '=' client=QualifiedName)? &
		('shared' '=' shared=QualifiedName)? &
		('server' '=' server=QualifiedName)?
	);


ImportElement: 'import' importedNamespace=QualifiedNameWithOptionalWildCard;


TranslationElement:'translation' name=QualifiedName (translations+=LanguageAttribute)+;
LanguageAttribute:lang=ID '=' text=STRING;


//TODO: code completion for placement, event
LogicElement: {LogicElement} 'logic' (name=ID)? (('event' '=' event=LogicEventType)? & ('placement' '=' placement=('client'|'server'|'inline'))? & ('exec' '=' exec=[LogicElement])?) ('{'source=STRING/*XBlockExpression*/'}')?;


CodeElement:'code' name=ID 
	(
		('id' '=' id=STRING) &
		('value_type' '=' valueType=STRING/*JvmTypeReference*/)? &
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' (logic+=LogicElement | codes+=CodeElement)* '}')?;


LookupElement:'lookup' name=ID '{' logic+=LogicElement* '}';


TemplateElement:'template' name=ID 'definition' '=' definition=STRING/*JvmTypeReference*/;



FormElement:'form' name=ID
	(
		('modal' '=' modal=BooleanType)? & 
		('columns' '=' columns=INT)? &
		('title' '=' title=[TranslationElement|QualifiedName])? &
		('subtitle' '=' subtitle=[TranslationElement|QualifiedName])? &
		('width_pixels' '=' widthInPixels=INT)? &
		('super_type' '=' superType=[TemplateElement])? &
		('svc_super_type' '=' svcSuperType=[TemplateElement])?
	)
	('{' (logic+=LogicElement | fields+=FormFieldElement | keyStrokes+=KeyElement)* '}')?;


// UI FIELDS

FormFieldElement:CompositeFieldElement | ValueFieldElement | CustomFieldElement | ButtonElement | TableElement | TabBoxElement;
NamedTypeElement:FormFieldElement | RadioButtonElement | CodeElement | FormElement;

// COMPOSITES
CompositeFieldElement:SequenceBoxElement|GroupBoxElement|TabElement;
SequenceBoxElement:'sequence' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? & 
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' (logic+=LogicElement | fields+=FormFieldElement)* '}')?;
	
GroupBoxElement:'group' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('columns' '=' columns=INT)? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('border_visible' '=' borderVisible=BooleanType)? &
		('border_decoration' '=' borderDecoration=('empty'|'line'|'section'|'auto'))? &
		('height' '=' gridHeight=INT)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' (logic+=LogicElement | fields+=FormFieldElement)* '}')?;

// VALUE FIELDS
GenericValueFieldElement:SmartfieldElement|ListBoxElement|RadioGroupElement;
ValueFieldElement: StringElement|BigDecimalElement|LongElement|DateElement|CheckboxElement|
	FileChooserElement|GenericValueFieldElement
;

StringElement:'string' name=ID 
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('maxlen' '=' maxlen=INT)? &
		('height' '=' gridHeight=INT)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('horizontal_align' '=' horizontalAlign=HorizontalAlignmentType)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' logic+=LogicElement* '}')?;


BigDecimalElement:'bigdecimal' name=ID 
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('percent' '=' percent=BooleanType)? &
		('grouping' '=' grouping=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('horizontal_align' '=' horizontalAlign=HorizontalAlignmentType)? &
		('fraction_digits' '=' fractionDigits=INT)? &
		('min' '=' min=Number)? &
		('max' '=' max=Number)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' logic+=LogicElement* '}')?;

LongElement:'long' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('horizontal_align' '=' horizontalAlign=HorizontalAlignmentType)? &
		('min' '=' min=INT)? &
		('max' '=' max=INT)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' logic+=LogicElement* '}')?;

DateElement:'date' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' logic+=LogicElement* '}')?;

SmartfieldElement:'smartfield' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('code' '=' code=[CodeElement])? &
		('value_type' '=' valueType=STRING/*JvmTypeReference*/)? &
		('lookup' '=' lookup=[LookupElement])? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' (logic+=LogicElement | menus+=MenuElement)* '}')?;


ListBoxElement:'list' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('code' '=' code=[CodeElement])? &
		('value_type' '=' valueType=STRING/*JvmTypeReference*/)? &
		('lookup' '=' lookup=[LookupElement])? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('height' '=' gridHeight=INT)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' (logic+=LogicElement)* '}')?;

// CUSTOM FIELDS
CustomFieldElement:'custom_field' name=ID 
	(
		('super_type' '=' superType=[TemplateElement]) &
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)?
	)
	('{' (logic+=LogicElement | fields+=FormFieldElement)* '}')?;

// BUTTONS
ButtonElement:'button' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('process_button' '=' processButton=BooleanType)? &
		('type' '=' type=('normal'|'cancel'|'close'|'ok'|'reset'|'save'))? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' (logic+=LogicElement | menus+=MenuElement)* '}')?;


// MENUS
MenuElement:'menu' name=ID
	(
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('single_select' '=' singleSelect=BooleanType)? &
		('multi_select' '=' multiSelect=BooleanType)? &
		('empty_select' '=' emptySelect=BooleanType)? &
		('type' '=' type=('normal'|'separator'|'cancel'))? &
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' (logic+=LogicElement | menus+=MenuElement)* '}')?;

TableElement:'table' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('height' '=' gridHeight=INT)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' (logic+=LogicElement | menus+=MenuElement | columns+=ColumnElement)* '}')?;

	
ColumnElement:'column' name=ID
	(
		('type' '=' type=('date'|'string'|'int'|'bigdecimal')) &
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('width' '=' width=INT)? &
		('visible' '=' visible=BooleanType)? &
		('displayable' '=' displayable=BooleanType)?
	);
	
KeyElement:'key' name=ID 'stroke' '=' stroke=STRING '{' logic+=LogicElement+ '}';

TabElement:'tab' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('columns' '=' columns=INT)? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	'{' (logic+=LogicElement | fields+=FormFieldElement)* '}';

TabBoxElement:'tabbox' name=ID
	(
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' (logic+=LogicElement | tabs+=TabElement)* '}')?;

CheckboxElement:'check' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' logic+=LogicElement* '}')?;
	
FileChooserElement:'filechooser' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('maxlen' '=' maxlen=INT)? &
		('mode' '=' mode=('directory'|'file'))? &
		('type' '=' type=('load'|'store'))? &
		('extensions' '=' extensions=STRING)? &
		('default_directory' '=' defaultDirectory=STRING)? &
		('show_directory' '=' showDirectory=BooleanType)? &
		('show_filename' '=' showFileName=BooleanType)? &
		('show_extension' '=' showFileExtension=BooleanType)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	('{' logic+=LogicElement* '}')?;
	
	
RadioGroupElement:'radio_group' name=ID
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('width' '=' gridWidth=INT)? &
		('width_pixels' '=' widthInPixels=INT)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('value_type' '=' valueType=STRING/*JvmTypeReference*/)? &
		('super_type' '=' superType=[TemplateElement])?
	)
	'{' (logic+=LogicElement | options+=RadioButtonElement)+ '}';


RadioButtonElement:'option' name=ID 
	(
		('text' '=' text=[TranslationElement|QualifiedName])? &
		('value' '=' value=STRING) &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('super_type' '=' superType=[TemplateElement])?
	);


